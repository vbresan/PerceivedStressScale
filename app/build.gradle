apply plugin: "com.android.application"

android {
    namespace "biz.binarysolutions.stress"
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "biz.binarysolutions.stress"
        minSdkVersion 21
        targetSdk 35
        versionCode 6
        versionName "1.1.3"

        setProperty("archivesBaseName", "$applicationId-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["ads"]
    productFlavors {
        withAds {
            dimension "ads"
        }
        withoutAds {
            dimension "ads"

            dependenciesInfo {
                includeInApk = false
                includeInBundle = false
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // version 1.7.0 causes Duplicate class exception in withoutAds flavor
    implementation "androidx.appcompat:appcompat:1.6.1"

    // version 23.6.0 causes another exception during the build
    withAdsImplementation "com.google.android.gms:play-services-ads:23.2.0"

    lintChecks "com.android.security.lint:lint:1.0.3"
}