apply plugin: "com.android.application"

android {
    namespace "biz.binarysolutions.stress"
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "biz.binarysolutions.stress"
        minSdkVersion 21
        targetSdk 35
        versionCode 6
        versionName "1.1.3"

        setProperty("archivesBaseName", "$applicationId-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions = ["ads"]
    productFlavors {
        withAds {
            dimension "ads"
        }
        withoutAds {
            dimension "ads"

            dependenciesInfo {
                includeInApk = false
                includeInBundle = false
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:1.7.1"

    // version 24.0.0+ requires min SDK 23
    withAdsImplementation "com.google.android.gms:play-services-ads:23.6.0"

    lintChecks "com.android.security.lint:lint:1.0.3"
}

/*
    Needed because including appcompat dependency causes duplicate class
    exceptions in withoutAds flavor.
*/
configurations.implementation {
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"
}